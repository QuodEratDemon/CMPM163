
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>demo - Phong shader</title>
		<meta charset="utf-8">
		<style>
			body {
			  	margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
<body>

<div id="container"></div>

    <script src="js/three.js"></script>
    <script src="js/cmgine.js"></script>
    <script id="vertexShader" type="x-shader/x-vertex">


	   varying vec2 vUv; 
    void main()
    {
        vUv = uv;

        vec4 mvPosition = modelViewMatrix * vec4(position, 1.0 );
        gl_Position = projectionMatrix * mvPosition;
    }

		</script>

		<script id="fragmentShader" type="x-shader/x-fragment">

	       precision mediump float;


	       uniform float iTime;
         uniform vec2 iResolution;
         uniform vec2 iTexRes;
         uniform sampler2D iChannel0;
        varying vec2 vUv;

        const int MAX_MARCHING_STEPS = 255;
        const float MIN_DIST = 0.0;
        const float MAX_DIST = 100.0;
        const float EPSILON = 0.0001;

  float intersectSDF(float distA, float distB) {
    return max(distA, distB);
}

float unionSDF(float distA, float distB) {
    return min(distA, distB);
}

float differenceSDF(float distA, float distB) {
    return max(distA, -distB);
}

float cubeSDF(vec3 p) {
    // If d.x < 0, then -1 < p.x < 1, and same logic applies to p.y, p.z
    // So if all components of d are negative, then p is inside the unit cube
    vec3 d = abs(p) - vec3(1.0+(abs(sin(iTime/4.0)/2.0)), 1.0+(abs(sin(iTime/4.0)/2.0)), 1.0+(abs(sin(iTime/4.0)/2.0)));
    
    // Assuming p is inside the cube, how far is it from the surface?
    // Result will be negative or zero.
    float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);
    
    // Assuming p is outside the cube, how far is it from the surface?
    // Result will be positive or zero.
    float outsideDistance = length(max(d, 0.0));
    
    return insideDistance + outsideDistance;
}

float sphereSDF(vec3 p) {
    return length(p) - abs((sin(iTime/2.0)/3.0)+1.0);


}

float sceneSDF(vec3 samplePoint) {
    float sphereDist = sphereSDF(samplePoint / 1.2) * 1.2;
    float cubeDist = cubeSDF(samplePoint + vec3(0.0, sin(iTime), 0.0));
    return differenceSDF(cubeDist, sphereDist);
}

float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
    float depth = start;
    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
        float dist = sceneSDF(eye + depth * marchingDirection);
        if (dist < EPSILON) {
      return depth;
        }
        depth += dist;
        if (depth >= end) {
            return end;
        }
    }
    return end;
}

vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
    vec2 xy = fragCoord - size / 2.0;
    float z = size.y / tan(radians(fieldOfView) / 2.0);
    return normalize(vec3(xy, -z));
}


vec3 estimateNormal(vec3 p) {
    return normalize(vec3(
        sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
        sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
        sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
    ));
}

vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
                          vec3 lightPos, vec3 lightIntensity) {
    vec3 N = estimateNormal(p);
    vec3 L = normalize(lightPos - p);
    vec3 V = normalize(eye - p);
    vec3 R = normalize(reflect(-L, N));
    
    float dotLN = dot(L, N);
    float dotRV = dot(R, V);
    
    if (dotLN < 0.0) {
        // Light not visible from this point on the surface
        return vec3(0.0, 0.0, 0.0);
    } 
    
    if (dotRV < 0.0) {
        // Light reflection in opposite direction as viewer, apply only diffuse
        // component
        return lightIntensity * (k_d * dotLN);
    }
    return lightIntensity * (k_d * dotLN + k_s * pow((dotRV), (alpha)));
}


vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
    const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
    vec3 color = ambientLight * k_a;
    
    vec3 light1Pos = vec3(4.0 * sin(iTime),
                          2.0,
                          4.0 * cos(iTime));
    vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                  light1Pos,
                                  light1Intensity);
    
    vec3 light2Pos = vec3(2.0 * sin(0.37 * iTime),
                          2.0 * cos(0.37 * iTime),
                          2.0);
    vec3 light2Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                  light2Pos,
                                  light2Intensity);    
    return color;
}

mat4 iviewMatrix(vec3 eye, vec3 center, vec3 up) {
    // Based on gluLookAt man page
    vec3 f = normalize(center - eye);
    vec3 s = normalize(cross(f, up));
    vec3 u = cross(s, f);
    return mat4(
        vec4(s, 0.0),
        vec4(u, 0.0),
        vec4(-f, 0.0),
        vec4(0.0, 0.0, 0.0, 1)
    );
}




      	void main() {

          vec3 viewDir = rayDirection(45.0, iResolution.xy, gl_FragCoord.xy);
          vec3 eye = vec3(8.0, 5.0, 7.0);
          
          mat4 viewToWorld =iviewMatrix(eye, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));
          
          vec3 worldDir = (viewToWorld * vec4(viewDir, 0.0)).xyz;
          
          float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);
          
          if (dist > MAX_DIST - EPSILON) {
              // Didn't hit anything
              gl_FragColor  = vec4(0.0, 0.0, 0.0, 0.0);
          return;
          }
          
          // The closest point on the surface to the eyepoint along the view ray
          vec3 p = eye + dist * worldDir;

          vec3 texMove = vec3(eye + MIN_DIST * worldDir);

          vec4 tex = texture2D(iChannel0, vec2((gl_FragCoord.x/ iResolution.x), (gl_FragCoord.y/ iResolution.y)+ (sin(iTime)/texMove.y))) ;

          
          
          vec3 K_a = vec3(tex.r* 0.2, tex.g* 0.2, tex.b* 0.2);
          //vec3 K_a = vec3(0.2, 0.2, 0.2); //vec3(tex.r/3.0, tex.g/3.0, tex.b/3.0);//vec3(0.2, 0.2, 0.2);
          vec3 K_d = tex.rgb;//vec3(0.1, 0.2, 0.2);
          vec3 K_s = vec3(1.0, 1.0, 1.0);
          float shininess = 18.0;


    
          vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
          vec3 col = vec3(mix(tex.xyz,color,0.8));

        
        	gl_FragColor = vec4(color, 1.0);
        

        }

	</script>


	<script>
				

  var container;
  var scene;
  var camera;
  var vs = document.getElementById( 'vertexShader' ).textContent;
  var fs = document.getElementById( 'fragmentShader' ).textContent;
  var tuniform;



	init();
	animate();

	function init() {

		container = document.getElementById( 'container' );
var width = window.innerWidth;
      var height = window.innerHeight;
		camera = new THREE.PerspectiveCamera( 75, window.innerWidth/window.innerHeight, 0.1, 1000 );
      camera.position.z = 5;
		scene = new THREE.Scene();

    var texRes = THREE.ImageUtils.loadTexture( 'angus.jpg');
    
    var tuniform = {
        iTime:    { type: 'f', value: 0.1 },
        iResolution: new THREE.Uniform(new THREE.Vector2(window.innerWidth, window.innerHeight)),
        iChannel0:  { type: 't', value: THREE.ImageUtils.loadTexture( 'angus.jpg') },
        iTexRes: new THREE.Uniform(new THREE.Vector2(texRes.width, texRes.height))

    };

    var plane = new THREE.PlaneBufferGeometry( window.innerWidth, window.innerHeight );
    var mat = new THREE.ShaderMaterial( {
            uniforms: tuniform,
            vertexShader: vs,
            fragmentShader: fs,            
            //side:THREE.DoubleSide
    } );
    var material = new THREE.MeshBasicMaterial( { color: 0x00ff00 } );
    var tobject = new THREE.Mesh( plane, mat);
    scene.add(tobject);


        	


		//make it so that resizing the browser window also resizes the scene


      renderer = new THREE.WebGLRenderer();
      renderer.setSize( window.innerWidth, window.innerHeight );
      //renderer.setPixelRatio( window.devicePixelRatio );
      //container.appendChild( renderer.domElement );

      document.body.appendChild( renderer.domElement );

    window.addEventListener( 'resize', onWindowResize, false );
	}

			

	function animate() {

		requestAnimationFrame( animate );
		render();

	}

	function render() {

		var time = performance.now();



		scene.children[0].material.uniforms.iTime.value = time/2000;//clock.getDelta();

		//if I want to update the lights, I acutally have to update the material used by each object in the scene. 
		//material.uniforms.light1_diffuse.value = new THREE.Vector3(0.0,1.0,0.0);

		renderer.render( scene, camera );
	}


	function onWindowResize( event ) {

		camera.aspect = window.innerWidth / window.innerHeight;
		camera.updateProjectionMatrix();

		renderer.setSize( window.innerWidth, window.innerHeight );

	}


		</script>

	</body>
</html>

